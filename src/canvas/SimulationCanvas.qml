import QtQuick 2.15
import QtQuick.Controls 2.15
import QtQuick.Layouts 1.15
import QtQuick.Shapes 1.15
import FluentUI
import "../simulation"
import "../section"

Rectangle {
    id: root
    width: parent.width
    height: parent.height
    color: "white"
    radius: 10
    border.color: "#a0a0a0"
    border.width: 2

    property bool canUpdate: true

    function throttledPaint() {
        if (canUpdate) {
            sensorCanvas.requestPaint()
            canUpdate = false
            Qt.callLater(function() {
                canUpdate = true
            })
        }
    }

    // Ê†áÈ¢ò
    FluText {
        x: 7
        y: 7
        text: "üïπÔ∏è Âπ≥Èù¢Âõæ"
        font.pixelSize: 20
        font.family: smileFont.name
        color: FluTheme.primaryColor
    }

    CarBody {
        id: carBody
        anchors.centerIn: parent
    }

    SensorRect {
        id: frontSensor
        direction: "front"
        x: carBody.x + carBody.width / 2 - width / 2
        y: carBody.y - height
    }

    SensorRect {
        id: backSensor
        direction: "back"
        x: carBody.x + carBody.width / 2 - width / 2
        y: carBody.y + carBody.height
    }

    SensorRect {
        id: leftSensor
        direction: "left"
        x: carBody.x - width
        y: carBody.y + carBody.height / 2 - height / 2
    }

    SensorRect {
        id: rightSensor
        direction: "right"
        x: carBody.x + carBody.width
        y: carBody.y + carBody.height / 2 - height / 2
    }

    // ËΩ¶ËΩÆ
    Repeater {
        model: 4
        Wheel {
            index: model.index
            centerX: carBody.x + carBody.width / 2
            centerY: carBody.y + carBody.height / 2
        }
    }

    // üß≠ ÂõõÊñπÂêëË∑ùÁ¶ªÊñáÊú¨ÔºàÁªëÂÆö DebugConsole ‰∏≠ÁöÑÂ±ûÊÄßÔºâ
    Text {
        text: simulationCanvas.frontDis + " cm"
        font.family: smileFont.name
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.top: parent.top
        anchors.topMargin: 30
        anchors.leftMargin: 10
        font.pixelSize: 18
        color: "#3a3a3a"
    }

    Text {
        text: simulationCanvas.backDis + " cm"
        font.family: smileFont.name
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.bottom: parent.bottom
        anchors.bottomMargin: 30
        anchors.rightMargin: 10
        font.pixelSize: 18
        color: "#3a3a3a"
    }

    Text {
        text: simulationCanvas.leftDis + " cm"
        font.family: smileFont.name
        anchors.verticalCenter: parent.verticalCenter
        anchors.verticalCenterOffset: 18
        anchors.left: parent.left
        anchors.leftMargin: 93
        font.pixelSize: 18
        color: "#3a3a3a"
    }

    Text {
        text: simulationCanvas.rightDis + " cm"
        font.family: smileFont.name
        anchors.verticalCenter: parent.verticalCenter
        anchors.verticalCenterOffset: -18
        anchors.right: parent.right
        anchors.rightMargin: 93
        font.pixelSize: 18
        color: "#3a3a3a"
    }

    Canvas {
        id: sensorCanvas
        anchors.fill: parent

        onPaint: {
            const ctx = getContext("2d")
            ctx.clearRect(0, 0, width, height)

            function getColor(dist) {
                const ratio = Math.min(1.0, dist / maxDistance)
                const r = Math.floor(255 * (1 - ratio))
                const g = Math.floor(255 * ratio)
                return `rgba(${r},${g},0,0.5)`
            }

            function drawSensorLine(distance, angleDeg, sensorX, sensorY) {
                const angleRad = angleDeg * Math.PI / 180
                const dx = Math.cos(angleRad)
                const dy = Math.sin(angleRad)

                const capAngle = angleRad + Math.PI / 2
                const capDx = Math.cos(capAngle)
                const capDy = Math.sin(capAngle)

                const offset = 10  // Ëµ∑ÂßãÂÅèÁßªÔºåËÆ©ÂÆÉÂú®Ê®°ÂùóÂ§ñ
                const length = maxDistance

                const startX = sensorX + dx * offset
                const startY = sensorY + dy * offset
                const endX = startX + dx * length
                const endY = startY + dy * length

                const capLength = Math.abs(startX - endX) * 0.2 + Math.abs(startY - endY) * 0.2 // Ê®™Êù†ÈïøÂ∫¶

                ctx.strokeStyle = getColor(distance)
                ctx.lineWidth = 6

                ctx.beginPath()

                // Ëµ∑ÂßãÊ®™Êù†ÔºàÊ®™ÂêëÔºâ
                ctx.moveTo(startX + capDx * capLength, startY + capDy * capLength)
                ctx.lineTo(startX, startY)

                // ‰∏ªÁ∫ø
                ctx.lineTo(endX, endY)

                // ÁªàÁÇπÊ®™Êù†ÔºàÊ®™ÂêëÔºâ
                ctx.lineTo(endX + capDx * capLength, endY + capDy * capLength)

                ctx.stroke()
            }

            drawSensorLine(simulationCanvas.frontDis, 270, frontSensor.x + 3, frontSensor.y + frontSensor.height - 5)
            drawSensorLine(simulationCanvas.rightDis, 0, rightSensor.x + 5, rightSensor.y + 3)
            drawSensorLine(simulationCanvas.backDis, 90, backSensor.x  - 3 + backSensor.width, backSensor.y + 5)
            drawSensorLine(simulationCanvas.leftDis, 180, leftSensor.x + leftSensor.width - 5, leftSensor.y + leftSensor.height - 3)
        }

        Connections {
            target: simulationCanvas
            function onFrontDisChanged() {
                throttledPaint()
            }
            function onBackDisChanged() {
                throttledPaint()
            }
            function onLeftDisChanged() {
                throttledPaint()
            }
            function onRightDisChanged() {
                throttledPaint()
            }
        }
    }
}
